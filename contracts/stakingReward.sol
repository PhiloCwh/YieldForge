// SPDX-License-Identifier: MIT

/*
使用说明
1.设置挖矿时间
2.转入rewardtoken
3.设置rewardtoken的总数量
*/
pragma solidity ^0.8;

contract stakingRewards {
    IERC20 public stakingToken;
    IERC20 public rewardsToken;

    address public owner;

    // Duration of rewards to be paid out (in seconds)
    uint public duration;
    // Timestamp of when the rewards finish
    uint public finishAt;
    // Minimum of last updated time and reward finish time
    uint public updatedAt;
    // Reward to be paid out per second
    uint public rewardRate;
    // Sum of (reward rate * dt * 1e18 / total supply)
    uint public rewardPerTokenStored;
    // User address => rewardPerTokenStored
    mapping(address => uint) public userRewardPerTokenPaid;
    // User address => rewards to be claimed
    mapping(address => uint) public rewards;

    // Total staked
    uint public totalSupply;
    // User address => staked amount
    mapping(address => uint) public balanceOf;
    mapping(address => uint) public burnBalanceOf;

    constructor(
        address _stakingToken,
        address _rewardToken,
        address _owner
    ) payable {
        owner = _owner;
        stakingToken = IERC20(_stakingToken);
        rewardsToken = IERC20(_rewardToken);
    }

    receive() external payable {}
    fallback() external payable {}

    modifier onlyOwner() {
        require(msg.sender == owner, "not authorized");
        _;
    }

    modifier updateReward(address _account) {
        rewardPerTokenStored = rewardPerToken();
        updatedAt = lastTimeRewardApplicable();

        if (_account != address(0)) {
            rewards[_account] = earned(_account);
            userRewardPerTokenPaid[_account] = rewardPerTokenStored;
        }

        _;
    }

    function transferOwnerShip(address _owner) public onlyOwner {
        owner = _owner;
    }

    function lastTimeRewardApplicable() public view returns (uint) {
        return _min(finishAt, block.timestamp);
    }

    function rewardPerToken() public view returns (uint) {
        if (totalSupply == 0) {
            return rewardPerTokenStored;
        }

        return
            rewardPerTokenStored +
            (rewardRate * (lastTimeRewardApplicable() - updatedAt) * 1e18) /
            totalSupply;
    }

    function resetToken(
        address _stakingToken,
        address _rewardToken
    ) public onlyOwner {
        stakingToken = IERC20(_stakingToken);
        rewardsToken = IERC20(_rewardToken);
    }

    function stake(uint _amount) external updateReward(msg.sender) {
        require(_amount > 0, "amount = 0");
        stakingToken.transferFrom(msg.sender, address(this), _amount);
        balanceOf[msg.sender] += _amount;
        totalSupply += _amount;
    }

    function stakeBurn(uint _amount) public updateReward(msg.sender) {
        require(_amount > 0, "amount = 0");
        stakingToken.transferFrom(msg.sender, address(this), _amount);
        balanceOf[msg.sender] += 10 * _amount;
        burnBalanceOf[msg.sender] += 10 * _amount;
        totalSupply += 10 * _amount;
    }

    function realBalanceOf(address _user) public view returns (uint256) {
        return balanceOf[_user] - burnBalanceOf[_user];
    }

    function withdraw(uint _amount) external updateReward(msg.sender) {
        require(_amount > 0, "amount = 0");
        require(_amount <= realBalanceOf(msg.sender), "exceed your balance");
        balanceOf[msg.sender] -= _amount;
        totalSupply -= _amount;
        stakingToken.transfer(msg.sender, _amount);
    }

    function earned(address _account) public view returns (uint) {
        return
            ((balanceOf[_account] *
                (rewardPerToken() - userRewardPerTokenPaid[_account])) / 1e18) +
            rewards[_account];
    }

    function getReward() external updateReward(msg.sender) {
        uint reward = rewards[msg.sender];
        if (reward > 0) {
            rewards[msg.sender] = 0;
            rewardsToken.transfer(msg.sender, reward);
        }
    }
    //设置可以挖矿的时间，单位s
    function setRewardsDuration(uint _duration) public onlyOwner {
        require(finishAt < block.timestamp, "reward duration not finished");
        duration = _duration;
    }
    //可获取的ERC20数量
    function notifyRewardAmount(
        uint _amount
    ) public onlyOwner updateReward(address(0)) {
        if (block.timestamp >= finishAt) {
            rewardRate = _amount / duration;
        } else {
            uint remainingRewards = (finishAt - block.timestamp) * rewardRate;
            rewardRate = (_amount + remainingRewards) / duration;
        }

        require(rewardRate > 0, "reward rate = 0");
        require(
            rewardRate * duration <= rewardsToken.balanceOf(address(this)),
            "reward amount > balance"
        );

        finishAt = block.timestamp + duration;
        updatedAt = block.timestamp;
    }

    function _min(uint x, uint y) private pure returns (uint) {
        return x <= y ? x : y;
    }

    function getBalanceOfContract() public view returns (uint) {
        return rewardsToken.balanceOf(address(this));
    }

    function withdrawRewardToken() external onlyOwner {
        require(block.timestamp >= finishAt, "still product rewardToken");
        rewardsToken.transfer(msg.sender, getBalanceOfContract());
    }

    function withdrawEth() external onlyOwner {
        address payable caller = payable(msg.sender);
        caller.transfer(address(this).balance);
    }
    function withdrawErc20Token(address _erc20) external onlyOwner {
        //require(block.timestamp >= finishAt, "still product rewardToken");
        IERC20 erc20 = IERC20(_erc20);
        erc20.transfer(msg.sender, erc20.balanceOf(address(this)));
    }
}

interface IERC20 {
    function totalSupply() external view returns (uint);

    function balanceOf(address account) external view returns (uint);

    function transfer(address recipient, uint amount) external returns (bool);

    function allowance(
        address owner,
        address spender
    ) external view returns (uint);

    function approve(address spender, uint amount) external returns (bool);

    function transferFrom(
        address sender,
        address recipient,
        uint amount
    ) external returns (bool);

    event Transfer(address indexed from, address indexed to, uint value);
    event Approval(address indexed owner, address indexed spender, uint value);
}
